AC_LANG_CPLUSPLUS

AC_SEARCH_LIBS([dlopen], [dl dld], [], [
  AC_MSG_ERROR([unable to find the dlopen() function])
])

AC_ARG_WITH([fesvr],
  [AS_HELP_STRING([--with-fesvr],
    [path to your fesvr installation if not in a standard location])],
  [
    LDFLAGS="-L$withval/lib $LDFLAGS"
    CPPFLAGS="-I$withval/include $CPPFLAGS"
  ]
)

AC_CHECK_LIB(fesvr, libfesvr_is_present, [], [AC_MSG_ERROR([libfesvr is required])], [-pthread])

AC_CHECK_LIB(pthread, pthread_create, [], [AC_MSG_ERROR([libpthread is required])])

AC_ARG_ENABLE([commitlog], AS_HELP_STRING([--enable-commitlog], [Enable commit log generation]))
AS_IF([test "x$enable_commitlog" = "xyes"], [
  AC_DEFINE([RISCV_ENABLE_COMMITLOG],,[Enable commit log generation])
])

AC_ARG_ENABLE([histogram], AS_HELP_STRING([--enable-histogram], [Enable PC histogram generation]))
AS_IF([test "x$enable_histogram" = "xyes"], [
  AC_DEFINE([RISCV_ENABLE_HISTOGRAM],,[Enable PC histogram generation])
])

AC_ARG_ENABLE([simpoint], AS_HELP_STRING([--enable-simpoint], [Enable Basic Block Vector generation for simpoint tool]))
AS_IF([test "x$enable_simpoint" = "xyes"], [
  AC_DEFINE([RISCV_ENABLE_SIMPOINT],,[Enable Basic Block Vector generation for simpoint tool])
])
